package io.edukativ.myskoolin.domain.timetabling.constraints;
    dialect "java"

import io.edukativ.myskoolin.domain.timetabling.Lesson;
import io.edukativ.myskoolin.domain.timetabling.TimeSlot;
import io.edukativ.myskoolin.domain.timetabling.SchoolClassTimeTable;
import io.edukativ.myskoolin.domain.subjects.Subject;
import org.optaplanner.core.api.score.buildin.hardmediumsoft.HardMediumSoftScoreHolder;
import java.util.List

global HardMediumSoftScoreHolder scoreHolder;

//----------------------------------------------------------------------------------------------------------

rule "Time slot overlapping another time slot"
    when
        $lesson1 : Lesson(timeSlot != null)
        $lesson2 : Lesson(this != $lesson1, timeSlot != null, $lesson2.isOverlapping($lesson1))
    then
        scoreHolder.penalize(kcontext, $lesson1.overlappingGap($lesson2));
end

//----------------------------------------------------------------------------------------------------------

rule "School room right type"
    when
        $lesson: Lesson(!hasRightSchoolRoom())
    then
        scoreHolder.penalize(kcontext, $lesson.getTimeSlot().durationInMinutes().intValue());
end

//----------------------------------------------------------------------------------------------------------

rule "Subject duration max by day - multiple lessons"
    when
        $lesson1 : Lesson( $subject1: subject )
        $lesson2 : Lesson( this != $lesson1, $subject2: subject, $subject2.id == $subject1.id, timeSlot.day == $lesson1.timeSlot.day )
        $totalDuration : Number() from accumulate(
                            TimeSlot( $duration : durationInMinutes(), $subject1.maxMinutesPerDay < $duration ),
                            sum( $duration ) )
    then
        scoreHolder.penalize(kcontext, $totalDuration.intValue() - $subject1.getMaxMinutesPerDay());
end

rule "Subject duration max by day - single lesson"
    when
        $lesson : Lesson( $subject: subject, exceedsDuration())
    then
        scoreHolder.penalize(kcontext, $lesson.getTimeSlot().durationInMinutes().intValue() - $subject.getMaxMinutesPerDay());
end
